namespace ca.mcgill.ecse439.pds.model;

class PizzaDeliveryManager
{
	singleton;
	
	1 <@>- 1..* Pizza pizzas;
	1 <@>- 1..* Ingredient ingredients;
	1 <@>- * Order orders;
}

class Pizza
{
	Double price; 
	
	* -> 1..* Ingredient;
	
	before constructor {
		if(allIngredients==null || allIngredients.length==0)
		{
			throw new RuntimeException("Pizza ingredients cannot be empty");
		}
	}
}

class MenuPizza
{
	isA Pizza;
	 
	unique String name; 
	Integer calorieCount;
	
	before constructor, setName {
		if(aName==null || aName.length()==0)
		{
			throw new RuntimeException("Pizza name cannot be empty");
		}
	}
}

class CustomPizza
{
	isA Pizza;
	const Double BASE_PRICE=3.0;
}

class Ingredient
{
	unique String name; 
	Double price;
	
	before constructor, setName {
		if(aName==null || aName.length()==0)
		{
			throw new RuntimeException("Ingredient name cannot be empty");
		}
	}
}

class Order
{
	enum OrderStatus{InProcess, Delivered}

    autounique orderId;
	String customerName;
	String phoneNumber;
	String email;
	String address;	
	OrderStatus status = OrderStatus.InProcess;
	
	Integer[] numberOfEachPizza;
    
    * -> 1..* Pizza;
    
    before constructor, setCustomerName {
    	if(aCustomerName==null || aCustomerName.length()==0)
    	{
    		throw new RuntimeException("Customer name cannot be empty");
    	}
    }
    
    before constructor,setAddress {
    	if(aAddress==null || aAddress.length()==0)
    	{
    		throw new RuntimeException("Customer address cannot be empty");
    	}
    }
    
    before constructor {
    	if((aPhoneNumber==null || aPhoneNumber.length()==0) && (aEmail==null || aEmail.length()==0))
    	{
    		throw new RuntimeException("At least one of customer's phone number and email should be provided.");
    	}
    }
    
    before setPhoneNumber{
    	if((aPhoneNumber==null || aPhoneNumber.length()==0)&&(email==null||email.length()==0)){
    		throw new RuntimeException("At least one of customer's phone number and email should be provided.");
    	}
    }
    
    before setEmail{
    	if((aEmail==null || aEmail.length()==0) && (phoneNumber==null || phoneNumber.length()==0)){
    		throw new RuntimeException("At least one of customer's phone number and email should be provided.");
    	}
    }
    	
}
